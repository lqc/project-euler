;; Solution to Project Euler problem 45
;; http://projecteuler.net/index.php?section=problems&id=45
;;
;; Triangle, pentagonal, and hexagonal numbers are generated by the
;; following formulae:
;;
;; Triangle	 	Tn=n * (n + 1) / 2     	1, 3, 6, 10, 15, ...
;; Pentagonal	 	Pn=n * (3 * n - 1) / 2 	1, 5, 12, 22, 35, ...
;; Hexagonal	 	Hn=n * (2 * n - 1)     	1, 6, 15, 28, 45, ...
;;
;; It can be verified that T_285 = P_165 = H_143 = 40755.
;;
;; Find the next triangle number that is also pentagonal and
;; hexagonal.
;;
;; NOTE:
;; All hexagonal numbers are triangle numbers, so we don't need
;; to check equality there.
;; See: http://en.wikipedia.org/wiki/Hexagonal_number
(ns com.stepniowski.euler-045)

(defn hexagonal-numbers []
  (map #(* % (dec (* 2 %))) (iterate inc 1)))

(defn pentagonal? [n]
  (zero? (rem (inc (Math/sqrt (inc (* 24 n)))) 6)))

(defn solution []
  (nth (filter pentagonal? (hexagonal-numbers)) 2))
